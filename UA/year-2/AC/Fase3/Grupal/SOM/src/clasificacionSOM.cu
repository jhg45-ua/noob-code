/*----------------------------------------------------------------------------*/
/*  FICHERO:       clasificacionSOM.cu									        */
/*  AUTOR:         Jorge Azorin								       			    */
/*													                            */
/*  RESUMEN												                        */
/*  ~~~~~~~												                        */
/* Ejercicio grupal para la clasificación de patrones de entrada basada         */
/* en SOM utilizando GPUs                                                       */
/*----------------------------------------------------------------------------*/

// includes, system
#include <stdlib.h>
#include <stdio.h>
#include <string.h>
#include <math.h>
#include <assert.h>


// includes, project
#include <cuda.h>
#include <cuda_runtime.h>
#include <device_launch_parameters.h>
#include "clasificacionSOM.h"

// --- ADAPTACIÓN MULTIPLATAFORMA ---
#if defined(_WIN32) || defined(_WIN64)
    #include <Windows.h>
    typedef LARGE_INTEGER timeStamp;
#else
    #include <sys/time.h>
    #include <stddef.h>
#endif
// ----------------------------------


/*----------------------------------------------------------------------------*/
/* PARÁMETROS DE MICROARQUITECTURA (Para tunear en el Cudathon)               */
/*----------------------------------------------------------------------------*/
// BLOCK_DIM_M3: Define la malla 2D de hilos para el cálculo de distancias.
// En Ampere (RTX 3050) 16x16 suele ir perfecto, pero en Turing (1660 Super)
// probar 32x8 o 32x16 para maximizar el uso de registros.
#define BLOCK_DIM_X_M3 16
#define BLOCK_DIM_Y_M3 16

// BLOCK_SIZE_M4: Define los hilos que participan en la Reducción en Árbol.
// Debe ser potencia de 2 (128, 256, 512). Modificar este valor altera la 
// ocupación (Occupancy) y la cantidad de Memoria Compartida reservada.
#define BLOCK_SIZE_M4 256
#define GRID_SIZE_M4 64


#define ERROR_CHECK { cudaError_t err; if ((err = cudaGetLastError()) != cudaSuccess) { printf("CUDA error: %s, line %d\n", cudaGetErrorString(err), __LINE__);}}

double getTime();

/*
 * Copia robusta preservada para rama de desarrollo:
 * centraliza la liberacion correcta de EtiquetaCPU/EtiquetaGPU y
 * anula punteros para evitar dobles liberaciones accidentales.
 */
void LiberarEtiquetasRobusto(int** etiquetaCPU, int** etiquetaGPU)
{
	if (etiquetaCPU != NULL && *etiquetaCPU != NULL)
	{
		free(*etiquetaCPU);
		*etiquetaCPU = NULL;
	}
	if (etiquetaGPU != NULL && *etiquetaGPU != NULL)
	{
		free(*etiquetaGPU);
		*etiquetaGPU = NULL;
	}
}

/*----------------------------------------------------------------------------*/
/* Helpers M3: indices row-major y distancia euclidea sobre arrays planos     */
/*----------------------------------------------------------------------------*/
__host__ __device__ inline int IndiceNeuronaRowMajor(int y, int x, int ancho)
{
	// Convenio comun del equipo: neurona (y,x) -> y*ancho + x
	return y * ancho + x;
}

__host__ __device__ inline int IndicePesoRowMajor(int indiceNeurona, int dimension, int componente)
{
	// Layout lineal de pesos: [neurona0(dim), neurona1(dim), ...]
	return indiceNeurona * dimension + componente;
}

__host__ __device__ inline int IndicePesosSoA(int indiceNeurona, int componente, int totalNeuronas)
{
	// Layout SoA (Structure of Arrays): [comp0(neuronas), comp1(neuronas), ...]
	return componente * totalNeuronas + indiceNeurona;
}

static void CopiarSOMLinealSoA(float* pesosLineales)
{
	int totalNeuronas = SOM.Alto * SOM.Ancho;

	for (int y = 0; y < SOM.Alto; y++) {
		for (int x = 0; x < SOM.Ancho; x++) {
			const int indiceNeurona = IndiceNeuronaRowMajor(y, x, SOM.Ancho);
			for (int d = 0; d < SOM.Dimension; d++) {
				// Usamos el nuevo mapeo SoA
				pesosLineales[IndicePesosSoA(indiceNeurona, d, totalNeuronas)] = SOM.Neurona[y][x].pesos[d];
			}
		}
	}
}

__device__ float DistanciaEuclideaNeurona(const float* pesosSOM, const float* patron, int indiceNeurona, int dimension, int totalNeuronas)
{
	float acumulado = 0.0f;
	#pragma unroll 4
	for (int d = 0; d < dimension; ++d)
	{
		const float diferencia = pesosSOM[IndicePesosSoA(indiceNeurona, d, totalNeuronas)] - patron[d];
		acumulado += diferencia * diferencia;
	}
	return sqrtf(acumulado);
}

/*----------------------------------------------------------------------------*/
/* Helper M4: Reducción Ninja a nivel de registros físicos (Warp Shuffle)     */
/*----------------------------------------------------------------------------*/
__device__ inline void warpReduceMin(float& local_min, int& local_idx) 
{
    // Hacemos log2(32) = 5 rondas de intercambio de registros
    for (int offset = 16; offset > 0; offset /= 2) 
    {
        // __shfl_down_sync desplaza el registro 'offset' posiciones hacia abajo
        // 0xffffffff significa que los 32 hilos del warp participan activamente
        float vec_dist = __shfl_down_sync(0xffffffff, local_min, offset);
        int vec_idx    = __shfl_down_sync(0xffffffff, local_idx, offset);
        
        // Cada hilo se queda con el mínimo entre su registro y el de su vecino
        if (vec_dist < local_min) 
        {
            local_min = vec_dist;
            local_idx = vec_idx;
        }
    }
}

/*----------------------------------------------------------------------------*/
/* Kernel M3 (Ximo): distancia(neurona) + distancia(vecindario cruz valido)   */
/* Se encarga de calcular las distancias de cada neurona al patron de entrada */
/*----------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------*/
/* Kernel M3.1: Cálculo Base (Cero redundancia matemática)                    */
/*----------------------------------------------------------------------------*/
__global__ void KernelDistanciaBase(
	const float* pesosSOM,
	const float* patron,
	int alto,
	int ancho,
	int dimension,
	float* distanciasBase,
	int totalNeuronas)
{
	const int x = blockIdx.x * blockDim.x + threadIdx.x;
	const int y = blockIdx.y * blockDim.y + threadIdx.y;

	if (x >= ancho || y >= alto) return;

	const int indiceNeurona = IndiceNeuronaRowMajor(y, x, ancho);

	// Pasamos 'patron' directo en lugar de 'sPatron'
	distanciasBase[indiceNeurona] = DistanciaEuclideaNeurona(pesosSOM, patron, indiceNeurona, dimension, totalNeuronas);
}

/*----------------------------------------------------------------------------*/
/* Kernel M3.2: Patrón Stencil (Suma del vecindario en cruz)                  */
/*----------------------------------------------------------------------------*/
__global__ void KernelSumaVecindario(
	const float* distanciasBase, // Recibe el array ya calculado por el M3.1
	int alto,
	int ancho,
	float* distanciasFinales)
{
	const int x = blockIdx.x * blockDim.x + threadIdx.x;
	const int y = blockIdx.y * blockDim.y + threadIdx.y;

	if (x >= ancho || y >= alto) return;

	const int indiceNeurona = IndiceNeuronaRowMajor(y, x, ancho);
	
	// Leemos nuestra propia distancia ya calculada
	float sumaDistancias = distanciasBase[indiceNeurona];

	// Sumamos las distancias precalculadas de los vecinos (Lectura directa de VRAM)
	if (y > 0) sumaDistancias += distanciasBase[IndiceNeuronaRowMajor(y - 1, x, ancho)];
	if (y < alto - 1) sumaDistancias += distanciasBase[IndiceNeuronaRowMajor(y + 1, x, ancho)];
	if (x > 0) sumaDistancias += distanciasBase[IndiceNeuronaRowMajor(y, x - 1, ancho)];
	if (x < ancho - 1) sumaDistancias += distanciasBase[IndiceNeuronaRowMajor(y, x + 1, ancho)];

	// Contrato de salida: puntuación total por neurona
	distanciasFinales[indiceNeurona] = sumaDistancias;
}

static void CopiarTodosPatronesLineales(float* todosPatronesLineales)
{
	for (int p = 0; p < Patrones.Cantidad; ++p)
	{
		for (int d = 0; d < Patrones.Dimension; ++d)
		{
			// Aplanamos: [patron0_d0, patron0_d1..., patron1_d0, patron1_d1...]
			todosPatronesLineales[p * Patrones.Dimension + d] = Patrones.Pesos[p][d];
		}
	}
}

static void CopiarEtiquetasLineal(int* labelsLineales)
{
	for (int y = 0; y < SOM.Alto; ++y)
		for (int x = 0; x < SOM.Ancho; ++x)
			labelsLineales[IndiceNeuronaRowMajor(y, x, SOM.Ancho)] = SOM.Neurona[y][x].label;
}

/*--------------------------------------------------------------------------------*/
/*    Kernel M4 (Julián): Reducción Naive (Búsqueda secuencial en 1 hilo)         */
/* Se encarga de encontrar el índice de la neurona ganadora (con menor distancia) */
/*--------------------------------------------------------------------------------*/

/*----------------------------------------------------------------------------*/
/* FASE 1: Reducción Local por Bloques                                        */
/*----------------------------------------------------------------------------*/
__global__ void KernelReduccionFase1(const float* distancias, int totalNeuronas, float* minDistIntermedio, int* minIdxIntermedio)
{
	__shared__ float sDist[BLOCK_SIZE_M4];
	__shared__ int sIndex[BLOCK_SIZE_M4];

	int tid = threadIdx.x;
	// El hilo global ahora usa gridDim.x para el salto (cubrimos la gráfica entera)
	int i = blockIdx.x * blockDim.x + threadIdx.x;
	int stride = blockDim.x * gridDim.x;

	float min_dist = 1e38f;
	int min_idx = -1;

	// 1. Cada hilo busca el mínimo de los elementos que le tocan
	for (; i < totalNeuronas; i += stride)
	{
		float d = distancias[i];
		if (d < min_dist)
		{
			min_dist = d;
			min_idx = i;
		}
	}

	sDist[tid] = min_dist;
	sIndex[tid] = min_idx;
	__syncthreads();

	// 2. Reducción en árbol (Torneo dentro del bloque)
	for (int s = blockDim.x / 2; s > 0; s >>= 1)
	{
		if (tid < s)
		{
			if (sDist[tid + s] < sDist[tid])
			{
				sDist[tid] = sDist[tid + s];
				sIndex[tid] = sIndex[tid + s];
			}
		}
		__syncthreads();
	}

	// 3. El hilo 0 de CADA BLOQUE escribe su ganador local en el array intermedio
	if (tid == 0)
	{
		minDistIntermedio[blockIdx.x] = sDist[0];
		minIdxIntermedio[blockIdx.x] = sIndex[0];
	}
}

/*----------------------------------------------------------------------------*/
/* Kernel M4 - FASE 2: Reducción Final del ganador absoluto                   */
/*----------------------------------------------------------------------------*/
__global__ void KernelReduccionFase2(const float* minDistIntermedio, const int* minIdxIntermedio, int numBloquesFase1, const int* labelsLineales, int* etiquetasSalida, int np)
{
    // ¡La memoria compartida se reduce drásticamente! Solo necesitamos espacio para 1 ganador por Warp
    __shared__ float sWarpDist[32]; 
    __shared__ int sWarpIndex[32];

    int tid = threadIdx.x;
    int laneId = tid % 32; // Qué hilo soy dentro de mi Warp (0 a 31)
    int warpId = tid / 32; // A qué Warp pertenezco (0 a 7 si el bloque es de 256)

    float min_dist = 1e38f;
    int min_idx = -1;

    // 1. Cada hilo carga su dato desde la memoria global (Resultados de la Fase 1)
    for (int i = tid; i < numBloquesFase1; i += blockDim.x)
    {
        float d = minDistIntermedio[i];
        if (d < min_dist)
        {
            min_dist = d;
            min_idx = minIdxIntermedio[i];
        }
    }

    // 2. Torneo a nivel de registros dentro del Warp (Cero latencia de memoria)
    warpReduceMin(min_dist, min_idx);

    // 3. El hilo 0 de cada Warp escribe SU campeón en la memoria compartida
    if (laneId == 0)
    {
        sWarpDist[warpId] = min_dist;
        sWarpIndex[warpId] = min_idx;
    }
    
    // ---------------------------------------------------------
    // LA ÚNICA BARRERA DE TODO EL KERNEL. Hemos pasado de 8 a 1.
    __syncthreads(); 
    // ---------------------------------------------------------

    // 4. El Warp 0 se encarga de la final
    if (warpId == 0)
    {
        // Los hilos del Warp 0 leen a los campeones. Si no hay campeón, leen infinito.
        min_dist = (tid < (blockDim.x / 32)) ? sWarpDist[laneId] : 1e38f;
        min_idx  = (tid < (blockDim.x / 32)) ? sWarpIndex[laneId] : -1;

        // Último torneo en registros
        warpReduceMin(min_dist, min_idx);

        // 5. El hilo absoluto 0 escribe la sentencia
        if (tid == 0) etiquetasSalida[np] = labelsLineales[min_idx];
    }
}

/*----------------------------------------------------------------------------*/
/*  FUNCION A PARALELIZAR  (versión secuencial-CPU)  				          */
/*	Implementa la clasificación basada en SOM de un conjunto de patrones      */
/*  de entrada definidos en un fichero                                        */
/*							!!!!! NO TOCAR !!!!!						      */
/*----------------------------------------------------------------------------*/
int ClasificacionSOMCPU()
{
	float distancia;
	float distanciaMenor=MAXDIST;

	for (int np=0;np<Patrones.Cantidad;np++) // Recorrido de todos los patrones
	{
		distanciaMenor=MAXDIST;
		for (int y = 0; y<SOM.Alto; y++)			// Recorrido de todas las neuronas
		{
			for (int x = 0; x<SOM.Ancho; x++)
			{
				distancia=CalculaDistancia(y,x,np);     // CalculaDistancia entre neurona (y,x) y patrón np
				for (int vy=-1;vy<2;vy++)               // Calculo en la vecindad
					for (int vx=-1;vx<2;vx++)
						if ((vx == 0) ^ (vy == 0))         // No comprobar con la misma neurona, se usa XOR en vez de AND para evirtar sumar solo las diagonales 
						   distancia+=CalculaDistancia(y+vy,x+vx,np);
				if (distancia < distanciaMenor)
				{
					distanciaMenor=distancia;  // Neurona con menor distancia
					EtiquetaCPU[np]=SOM.Neurona[y][x].label;
				}

			}
		}

	}
	return OKCLAS;									// Simulación CORRECTA
}

// ---------------------------------------------------------------
// ---------------------------------------------------------------
// FUNCION A IMPLEMENTAR POR EL GRUPO (paralelización de ClasificacionSOMCPU)
// ---------------------------------------------------------------
// ---------------------------------------------------------------

int ClasificacionSOMGPU()
{
	int estado_final = OKCLAS;

	// 1. Declaración de punteros
	float* hPesosLineales = NULL;
	float* hTodosPatrones = NULL; // Todos los patrones
	int* hLabelsLineales = NULL; 

	float* dPesosLineales = NULL;
	float* dTodosPatrones = NULL; // Todos los patrones en Device
	int* dLabelsLineales = NULL;  // Etiquetas del mapa en Device
	float* d_DistanciasBase = NULL; // Array temporal para el patrón Stencil
	float* dDistancias = NULL;
	int* dEtiquetasSalida = NULL; // Array de resultados en Device
	float* d_minDistIntermedio = NULL;
	int* d_minIdxIntermedio = NULL;

	fprintf(stderr, "Iniciando ClasificacionSOMGPU...\n");
	if (Patrones.Dimension != SOM.Dimension) return ERRORCLASS;

	fprintf(stderr, "SOM: %dx%d, Dimension: %d\n", SOM.Alto, SOM.Ancho, SOM.Dimension);
	const int totalNeuronas = SOM.Alto * SOM.Ancho;
	const size_t bytesPesos = (size_t)totalNeuronas * (size_t)SOM.Dimension * sizeof(float);
	const size_t bytesLabels = (size_t)totalNeuronas * sizeof(int);
	
	// Calculamos el tamaño para TODOS los patrones y TODAS las etiquetas de salida
	const size_t bytesTodosPatrones = (size_t)Patrones.Cantidad * (size_t)SOM.Dimension * sizeof(float);
	const size_t bytesEtiquetasSalida = (size_t)Patrones.Cantidad * sizeof(int);
	const size_t bytesDistancias = (size_t)totalNeuronas * sizeof(float);

	const size_t bytesIntermedios = GRID_SIZE_M4 * sizeof(float);
	const size_t bytesIdxIntermedios = GRID_SIZE_M4 * sizeof(int);

	// 2. Reserva en Host
	hPesosLineales = (float*)malloc(bytesPesos);
	hTodosPatrones = (float*)malloc(bytesTodosPatrones);
	hLabelsLineales = (int*)malloc(bytesLabels);

	if (hPesosLineales == NULL || hTodosPatrones == NULL || hLabelsLineales == NULL) estado_final = ERRORCLASS;

	if (estado_final == OKCLAS)
	{
		// Copiamos TODOS los datos al formato lineal de golpe
		CopiarSOMLinealSoA(hPesosLineales);
		CopiarTodosPatronesLineales(hTodosPatrones); 
		CopiarEtiquetasLineal(hLabelsLineales);

		// 3. Reserva masiva en Device (GPU)
		if (cudaMalloc((void**)&dPesosLineales, bytesPesos) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&dTodosPatrones, bytesTodosPatrones) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&dLabelsLineales, bytesLabels) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&dDistancias, bytesDistancias) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&d_DistanciasBase, bytesDistancias) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&dEtiquetasSalida, bytesEtiquetasSalida) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&d_minDistIntermedio, bytesIntermedios) != cudaSuccess) estado_final = ERRORCLASS;
		if (cudaMalloc((void**)&d_minIdxIntermedio, bytesIdxIntermedios) != cudaSuccess) estado_final = ERRORCLASS;
	}

	if (estado_final == OKCLAS)
	{
		// 4. UNA ÚNICA COPIA MASIVA: Host -> Device
		cudaMemcpy(dPesosLineales, hPesosLineales, bytesPesos, cudaMemcpyHostToDevice);
		cudaMemcpy(dTodosPatrones, hTodosPatrones, bytesTodosPatrones, cudaMemcpyHostToDevice);
		cudaMemcpy(dLabelsLineales, hLabelsLineales, bytesLabels, cudaMemcpyHostToDevice);

		// Uso de los parámetros de microarquitectura definidos en cabecera
		const dim3 blockDimM3(BLOCK_DIM_X_M3, BLOCK_DIM_Y_M3);
		const dim3 gridDimM3((SOM.Ancho + blockDimM3.x - 1) / blockDimM3.x, (SOM.Alto + blockDimM3.y - 1) / blockDimM3.y);

		// 5. El bucle ahora es ultrarrápido: solo encola trabajo en la GPU
		for (int np = 0; np < Patrones.Cantidad && estado_final == OKCLAS; ++np)
		{
			// Puntero aritmético para decirle al kernel dónde empieza el patrón 'np'
			float* dPatronActual = &dTodosPatrones[np * SOM.Dimension];

			// --- FASE M3.1: Cálculo Base (ALU Bound) ---
			KernelDistanciaBase << <gridDimM3, blockDimM3 >> > (
				dPesosLineales, dPatronActual, SOM.Alto, SOM.Ancho, SOM.Dimension, d_DistanciasBase, totalNeuronas);

			// --- FASE M3.2: Stencil (Memory Bound ultrarrápido) ---
			// No necesita memoria compartida
			KernelSumaVecindario<<<gridDimM3, blockDimM3>>>(
				d_DistanciasBase, SOM.Alto, SOM.Ancho, dDistancias);

			// FASE M4.1: Reducción Multibloque (Satura los SMs)
			KernelReduccionFase1<<<GRID_SIZE_M4, BLOCK_SIZE_M4>>>(
				dDistancias, totalNeuronas, d_minDistIntermedio, d_minIdxIntermedio);

			// FASE M4.2: Colapso al resultado final (1 solo bloque rápido)
			KernelReduccionFase2<<<1, BLOCK_SIZE_M4>>>(
				d_minDistIntermedio, d_minIdxIntermedio, GRID_SIZE_M4, dLabelsLineales, dEtiquetasSalida, np);
		}

		// Sincronizamos para asegurar que todos los kernels han terminado
		cudaDeviceSynchronize();

		// 6. UNA ÚNICA COPIA MASIVA DE VUELTA: Device -> Host
		if (cudaMemcpy(EtiquetaGPU, dEtiquetasSalida, bytesEtiquetasSalida, cudaMemcpyDeviceToHost) != cudaSuccess) {
			estado_final = ERRORCLASS;
		}
	}

	// 7. Limpieza de memoria
	if (dDistancias) cudaFree(dDistancias);
	if (d_DistanciasBase) cudaFree(d_DistanciasBase);
	if (dTodosPatrones) cudaFree(dTodosPatrones);
	if (dPesosLineales) cudaFree(dPesosLineales);
	if (dLabelsLineales) cudaFree(dLabelsLineales);
	if (dEtiquetasSalida) cudaFree(dEtiquetasSalida);
	if (d_minDistIntermedio) cudaFree(d_minDistIntermedio);
	if (d_minIdxIntermedio) cudaFree(d_minIdxIntermedio);
	if (hTodosPatrones) free(hTodosPatrones);
	if (hPesosLineales) free(hPesosLineales);
	if (hLabelsLineales) free(hLabelsLineales);

	return estado_final;
}
// ---------------------------------------------------------------
 // ---------------------------------------------------------------
 // ---------------------------------------------------------------
 // ---------------------------------------------------------------
 // ---------------------------------------------------------------

 // Declaraciones adelantadas de funciones
 int LeerSOM(const char *fichero);
 int LeerPatrones(const char *fichero);
 int EscribirSOM(int ancho, int alto,int dimension, const char *fichero);
 int EscribirPatrones(int cantidad,int dimension, const char *fichero);



////////////////////////////////////////////////////////////////////////////////
//PROGRAMA PRINCIPAL
/*							!!!!! NO TOCAR !!!!!						      */
////////////////////////////////////////////////////////////////////////////////
void
runTest(int argc, char** argv)
{

  	double gpu_start_time, gpu_end_time;
	double cpu_start_time, cpu_end_time;

	//EscribirSOM(32,32,64,"peq.som");
	//EscribirPatrones(1024,64,"peq.pat");
	/* Numero de argumentos */
	if (argc != 3)
	{
		fprintf(stderr, "Numero de parametros incorecto\n");
		fprintf(stderr, "Uso: %s superficie pasossim\n", argv[0]);
		return;
	}

	/* Apertura de Fichero */
	printf("Clasificacion basada en SOM...\n");
	/* Mapa SOM */
	if (LeerSOM((char *)argv[1]) == ERRORCLASS)
	{
		fprintf(stderr, "Lectura de SOM incorrecta\n");
		return;
	}
	/* Patrones */
	if (LeerPatrones((char *)argv[2]) == ERRORCLASS)
	{
		fprintf(stderr, "Lectura de patrones incorrecta\n");
		return;
	}
	
	// Creación etiquetas resultados para versiones CPU y GPU

	EtiquetaCPU = (int*)malloc(Patrones.Cantidad*sizeof(int));
	EtiquetaGPU = (int*)malloc(Patrones.Cantidad*sizeof(int));
	
	/* Algoritmo a paralelizar */
	cpu_start_time = getTime();
	if (ClasificacionSOMCPU() == ERRORCLASS)
	{
		fprintf(stderr, "Clasificacion CPU incorrecta\n");
		BorrarMapa();
		LiberarEtiquetasRobusto(&EtiquetaCPU, &EtiquetaGPU);
		exit(1);
	}
	cpu_end_time = getTime();
	cudaSetDevice(0);
	/* Algoritmo a implementar */
	gpu_start_time = getTime();
	if (ClasificacionSOMGPU() == ERRORCLASS)
	{
		fprintf(stderr, "Clasificacion GPU incorrecta\n");
		BorrarMapa();
		LiberarEtiquetasRobusto(&EtiquetaCPU, &EtiquetaGPU);
		return;
	}
	cudaDeviceSynchronize();
	gpu_end_time = getTime();
	// Comparación de corrección
	int comprobar = OKCLAS;
	for (int i = 0; i<Patrones.Cantidad; i++)
	{
		if ((EtiquetaCPU[i] != EtiquetaGPU[i]))
		{
			comprobar = ERRORCLASS;
			fprintf(stderr, "Fallo en la clasificacion del patron %d, valor correcto %d\n", i, EtiquetaCPU[i]);
		}
	}
	// Impresion de resultados
	if (comprobar == OKCLAS)
	{
		printf("Clasificacion correcta!\n");

	}
	// Impresión de resultados
	printf("Tiempo ejecucion GPU : %fs\n", \
		gpu_end_time - gpu_start_time);
	printf("Tiempo de ejecucion en la CPU : %fs\n", \
		cpu_end_time - cpu_start_time);
	printf("Se ha conseguido un factor de aceleracion %fx utilizando CUDA\n", (cpu_end_time - cpu_start_time) / (gpu_end_time - gpu_start_time));
	// Limpieza de Neuronas
	BorrarMapa();
	BorrarPatrones();
	LiberarEtiquetasRobusto(&EtiquetaCPU, &EtiquetaGPU);
	return;
}

int
main(int argc, char** argv)
{
	runTest(argc, argv);
	// getchar();
}

/* Funciones auxiliares */
double getTime()
{
#if defined(_WIN32) || defined(_WIN64)
    // Implementación original para Windows
    timeStamp start;
    timeStamp dwFreq;
    QueryPerformanceFrequency(&dwFreq);
    QueryPerformanceCounter(&start);
    return double(start.QuadPart) / double(dwFreq.QuadPart);
#else
    // Implementación para sistemas POSIX (Linux/macOS)
    struct timeval tv;
    gettimeofday(&tv, NULL);
    return (double)tv.tv_sec + (double)tv.tv_usec / 1000000.0;
#endif
}



/*----------------------------------------------------------------------------*/
/*	Función:  LeerSOM(char *fichero)						             	  */
/*													                          */
/*	          Lee la estructura del SOM con formato .SOM   					  */
/*							!!!!! NO TOCAR !!!!!						      */
/*----------------------------------------------------------------------------*/
int LeerSOM(const char *fichero)
{
	int i, j, ndim, count;		/* Variables de bucle */
	int alto,ancho;		/* Variables de tamaño del mapa */
	FILE *fpin; 			/* Fichero */
	int nx,ny,lx,ly,label,dimension;
	float pesos;

	/* Apertura de Fichero */
	if ((fpin = fopen(fichero, "r")) == NULL) return ERRORCLASS;
	/* Lectura de cabecera */
	if (fscanf(fpin, "Alto: %d\n", &alto)<0) return ERRORCLASS;
	if (fscanf(fpin, "Ancho: %d\n", &ancho)<0) return ERRORCLASS;
	if (fscanf(fpin, "Dimension: %d\n", &dimension)<0) return ERRORCLASS;
	if (feof(fpin)) return ERRORCLASS;

	if (CrearMapa(alto, ancho, dimension) == ERRORCLASS) return ERRORCLASS;
	/* Lectura del SOM */
	count = 0;
	for (i = 0; i<ancho; i++)
	{
		for (j = 0; j<alto; j++)
		{
			if (!feof(fpin))
			{
				fscanf(fpin, "N%d,%d:", &nx, &ny);
				for (ndim = 0; ndim<dimension-1;ndim++)
				{
			        fscanf(fpin, " %f", &pesos);
				    SOM.Neurona[j][i].pesos[ndim] = pesos;
				}
				fscanf(fpin, " %f\n", &pesos);
				SOM.Neurona[j][i].pesos[ndim] = pesos;
				
				fscanf(fpin, "L%d,%d: %d\n", &lx, &ly, &label);
				SOM.Neurona[j][i].label=label;
			
				count++;
			}
			else break;
		}
	}
	fclose(fpin);
	if (count != ancho*alto) return ERRORCLASS;
	return OKCLAS;
}

/*----------------------------------------------------------------------------*/
/*	Función:  LeerPatrones(char *fichero)						              */
/*													                          */
/*	          Lee los patrones de un fichero de entrada .pat   			      */
/*							!!!!! NO TOCAR !!!!!						      */
/*----------------------------------------------------------------------------*/
int LeerPatrones(const char *fichero)
{
	int i, ndim, count;		/* Variables de bucle */
	int cantidad,dimension;		/* Variables de tamaño de los patrones */
	FILE *fpin; 			/* Fichero */

	int np;
	float pesos;

	/* Apertura de Fichero */
	if ((fpin = fopen(fichero, "r")) == NULL) return ERRORCLASS;
	/* Lectura de cabecera */
	if (fscanf(fpin, "Numero: %d\n", &cantidad)<0) return ERRORCLASS;
	if (fscanf(fpin, "Dimension: %d\n", &dimension)<0) return ERRORCLASS;
	if (feof(fpin)) return ERRORCLASS;
	
	if (CrearPatrones(cantidad, dimension) == ERRORCLASS) return ERRORCLASS;
	/* Lectura de patrones */
	count = 0;
	for (i = 0; i<cantidad; i++)
	{
	  	if (!feof(fpin))
		{
			fscanf(fpin, "P%d:", &np);
				for (ndim = 0; ndim<dimension-1;ndim++)
				{
			        fscanf(fpin, " %f", &pesos);
					Patrones.Pesos[i][ndim] = pesos;
				}
				fscanf(fpin, " %f\n", &pesos);
				Patrones.Pesos[i][ndim] = pesos;
				
					
				count++;
			}
			else break;
	}
	
	fclose(fpin);
	if (count != cantidad) return ERRORCLASS;
	return OKCLAS;
}

/*----------------------------------------------------------------------------*/
/*	Función:  EscribirSOM(char *fichero)						              */
/*													                          */
/*	          Escribe la estructura del SOM en un fichero de salida .SOM   	  */
/*							!!!!! NO TOCAR !!!!!						      */
/*----------------------------------------------------------------------------*/
int EscribirSOM(int alto, int ancho, int dimension,const char *fichero)
{
	int i, j, ndim, count;		/* Variables de bucle */
	
	FILE *fpin; 			/* Fichero */
	int label;
	float pesos;

	/* Apertura de Fichero */
	if ((fpin = fopen(fichero, "w")) == NULL) return ERRORCLASS;
	/* Lectura de cabecera */
	if (fprintf(fpin, "Alto: %d\n", alto)<0) return ERRORCLASS;
	if (fprintf(fpin, "Ancho: %d\n", ancho)<0) return ERRORCLASS;
	if (fprintf(fpin, "Dimension: %d\n", dimension)<0) return ERRORCLASS;
	if (feof(fpin)) return ERRORCLASS;

	count = 0;
	for (i = 0; i<alto; i++)
	{
		for (j = 0; j<ancho; j++)
		{
			if (!feof(fpin))
			{
				fprintf(fpin, "N%d,%d:", i+1, j+1);
				for (ndim = 0; ndim<dimension-1;ndim++)
				{
					pesos=(rand()%1000)/10.0;
			        fprintf(fpin, " %lf", pesos);
				}				
				pesos=(rand()%1000)/10;
				fprintf(fpin, " %lf\n", &pesos);
				
				label=(rand()%5);
				fprintf(fpin, "L%d,%d: %d\n", i+1, j+1, label);
				
				count++;
			}
			else break;
		}
	}
	fclose(fpin);
	if (count != ancho*alto) return ERRORCLASS;
	return OKCLAS;
}


/*----------------------------------------------------------------------------*/
/*	Función:  EscribirPatrones(char *fichero)						          */
/*													                          */
/*	          Escribe los patrones en un fichero de salida .pat   	          */
/*							!!!!! NO TOCAR !!!!!						      */
/*----------------------------------------------------------------------------*/
int EscribirPatrones(int cantidad, int dimension,const char *fichero)
{
	int i, ndim, count;		/* Variables de bucle */
	
	FILE *fpin; 			/* Fichero */

	// int np;				// FIX NUESTRO: np no se usa, lo eliminamos para evitar confusión
	float pesos;

	/* Apertura de Fichero */
	if ((fpin = fopen(fichero, "w")) == NULL) return ERRORCLASS;
	/* Lectura de cabecera */
	if (fprintf(fpin, "Numero: %d\n", cantidad)<0) return ERRORCLASS;
	if (fprintf(fpin, "Dimension: %d\n", dimension)<0) return ERRORCLASS;
	if (feof(fpin)) return ERRORCLASS;
	
	/* Lectura de patrones */
	count = 0;
	for (i = 0; i<cantidad; i++)
	{
	  	if (!feof(fpin))
		{
			fprintf(fpin, "P%d:", i+1);
				for (ndim = 0; ndim<dimension-1;ndim++)
				{
					pesos=(rand()%1000)/10.0;
			        fprintf(fpin, " %lf", pesos);
				}
				pesos=(rand()%1000)/10;
				fprintf(fpin, " %lf\n", pesos);
					
					
				count++;
			}
			else break;
	}
	
	fclose(fpin);
	if (count != cantidad) return ERRORCLASS;
	return OKCLAS;
}
